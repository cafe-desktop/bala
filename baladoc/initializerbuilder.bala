/* initializerbuilder.bala
 *
 * Copyright (C) 2011  Florian Brosch
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301  USA
 *
 * Author:
 * 	Florian Brosch <flo.brosch@gmail.com>
 */


using Baladoc.Content;

private class Baladoc.Api.InitializerBuilder : Bala.CodeVisitor {
	private Bala.HashMap<Bala.Symbol, Symbol> symbol_map;
	private SignatureBuilder signature;

	private Symbol? resolve (Bala.Symbol symbol) {
		return symbol_map.get (symbol);
	}

	private void write_node (Bala.Symbol vsymbol) {
		signature.append_symbol (resolve (vsymbol));
	}

	private void write_type (Bala.DataType vsymbol) {
		if (vsymbol.type_symbol != null) {
			write_node (vsymbol.type_symbol);
		} else {
			signature.append_literal ("null");
		}

		var type_args = vsymbol.get_type_arguments ();
		if (type_args.size > 0) {
			signature.append ("<");
			bool first = true;
			foreach (Bala.DataType type_arg in type_args) {
				if (!first) {
					signature.append (",");
				} else {
					first = false;
				}
				if (!type_arg.value_owned) {
					signature.append_keyword ("weak");
				}
				signature.append (type_arg.to_qualified_string (null));
			}
			signature.append (">");
		}

		if (vsymbol.nullable) {
			signature.append ("?");
		}
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_array_creation_expression (Bala.ArrayCreationExpression expr) {
		signature.append_keyword ("new");
		write_type (expr.element_type);
		signature.append ("[", false);

		bool first = true;
		foreach (Bala.Expression size in expr.get_sizes ()) {
			if (!first) {
				signature.append (", ", false);
			}
			size.accept (this);
			first = false;
		}

		signature.append ("]", false);

		if (expr.initializer_list != null) {
			signature.append (" ", false);
			expr.initializer_list.accept (this);
		}
	}

	public InitializerBuilder (SignatureBuilder signature, Bala.HashMap<Bala.Symbol, Symbol> symbol_map) {
		this.symbol_map = symbol_map;
		this.signature = signature;
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_binary_expression (Bala.BinaryExpression expr) {
		expr.left.accept (this);
		if (expr.operator == Bala.BinaryOperator.IN) {
			signature.append_keyword (Bala.BinaryOperator.IN.to_string ());
		} else {
			signature.append (expr.operator.to_string ());
		}
		signature.append (" ");
		expr.right.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_unary_expression (Bala.UnaryExpression expr) {
		if (expr.operator == Bala.UnaryOperator.REF || expr.operator == Bala.UnaryOperator.OUT) {
			signature.append_keyword (expr.operator.to_string ());
		} else {
			signature.append (expr.operator.to_string ());
		}
		expr.inner.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_assignment (Bala.Assignment a) {
		a.left.accept (this);
		signature.append (a.operator.to_string ());
		a.right.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_cast_expression (Bala.CastExpression expr) {
		if (expr.is_non_null_cast) {
			signature.append ("(!)");
			expr.inner.accept (this);
			return;
		}

		if (!expr.is_silent_cast) {
			signature.append ("(", false);
			write_type (expr.type_reference);
			signature.append (")", false);
		}

		expr.inner.accept (this);

		if (expr.is_silent_cast) {
			signature.append_keyword ("as");
			write_type (expr.type_reference);
		}
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_initializer_list (Bala.InitializerList list) {
		signature.append ("{", false);

		bool first = true;
		foreach (Bala.Expression initializer in list.get_initializers ()) {
			if (!first) {
				signature.append (", ", false);
			}
			first = false;
			initializer.accept (this);
		}

		signature.append ("}", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_member_access (Bala.MemberAccess expr) {
		if (expr.symbol_reference != null) {
			expr.symbol_reference.accept (this);
		} else {
			signature.append (expr.member_name);
		}
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_element_access (Bala.ElementAccess expr) {
		expr.container.accept (this);
		signature.append ("[", false);

		bool first = true;
		foreach (Bala.Expression index in expr.get_indices ()) {
			if (!first) {
				signature.append (", ", false);
			}
			first = false;

			index.accept (this);
		}

		signature.append ("]", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_pointer_indirection (Bala.PointerIndirection expr) {
		signature.append ("*", false);
		expr.inner.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_addressof_expression (Bala.AddressofExpression expr) {
		signature.append ("&", false);
		expr.inner.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_reference_transfer_expression (Bala.ReferenceTransferExpression expr) {
		signature.append ("(", false).append_keyword ("owned", false).append (")", false);
		expr.inner.accept (this);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_type_check (Bala.TypeCheck expr) {
		expr.expression.accept (this);
		signature.append_keyword ("is");
		write_type (expr.type_reference);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_method_call (Bala.MethodCall expr) {
		// symbol-name:
		expr.call.symbol_reference.accept (this);

		// parameters:
		signature.append (" (", false);
		bool first = true;
		foreach (Bala.Expression literal in expr.get_argument_list ()) {
			if (!first) {
				signature.append (", ", false);
			}

			literal.accept (this);
			first = false;
		}
		signature.append (")", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_slice_expression (Bala.SliceExpression expr) {
		expr.container.accept (this);
		signature.append ("[", false);
		expr.start.accept (this);
		signature.append (":", false);
		expr.stop.accept (this);
		signature.append ("]", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_base_access (Bala.BaseAccess expr) {
		signature.append_keyword ("base", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_postfix_expression (Bala.PostfixExpression expr) {
		expr.inner.accept (this);
		if (expr.increment) {
			signature.append ("++", false);
		} else {
			signature.append ("--", false);
		}
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_object_creation_expression (Bala.ObjectCreationExpression expr) {
		if (!expr.struct_creation) {
			signature.append_keyword ("new");
		}

		signature.append_symbol (resolve (expr.symbol_reference));

		signature.append (" (", false);

		//TODO: rm conditional space
		bool first = true;
		foreach (Bala.Expression arg in expr.get_argument_list ()) {
			if (!first) {
				signature.append (", ", false);
			}
			arg.accept (this);
			first = false;
		}

		signature.append (")", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_sizeof_expression (Bala.SizeofExpression expr) {
		signature.append_keyword ("sizeof", false).append (" (", false);
		write_type (expr.type_reference);
		signature.append (")", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_typeof_expression (Bala.TypeofExpression expr) {
		signature.append_keyword ("typeof", false).append (" (", false);
		write_type (expr.type_reference);
		signature.append (")", false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_lambda_expression (Bala.LambdaExpression expr) {
		signature.append ("(", false);

		bool first = true;
		foreach (Bala.Parameter param in expr.get_parameters ()) {
			if (!first) {
				signature.append (", ", false);
			}
			signature.append (param.name, false);
			first = false;
		}


		signature.append (") => {", false);
		signature.append_highlighted (" [...] ", false);
		signature.append ("}", false);
	}



	/**
	 * {@inheritDoc}
	 */
	public override void visit_boolean_literal (Bala.BooleanLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_character_literal (Bala.CharacterLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_integer_literal (Bala.IntegerLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_real_literal (Bala.RealLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_regex_literal (Bala.RegexLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_string_literal (Bala.StringLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_null_literal (Bala.NullLiteral lit) {
		signature.append_literal (lit.to_string (), false);
	}



	/**
	 * {@inheritDoc}
	 */
	public override void visit_field (Bala.Field field) {
		write_node (field);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_constant (Bala.Constant constant) {
		write_node (constant);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_enum_value (Bala.EnumValue ev) {
		write_node (ev);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_error_code (Bala.ErrorCode ec) {
		write_node (ec);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_delegate (Bala.Delegate d) {
		write_node (d);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_method (Bala.Method m) {
		write_node (m);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_creation_method (Bala.CreationMethod m) {
		write_node (m);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_signal (Bala.Signal sig) {
		write_node (sig);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_class (Bala.Class c) {
		write_node (c);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_struct (Bala.Struct s) {
		write_node (s);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_interface (Bala.Interface i) {
		write_node (i);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_enum (Bala.Enum en) {
		write_node (en);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_error_domain (Bala.ErrorDomain ed) {
		write_node (ed);
	}

	/**
	 * {@inheritDoc}
	 */
	public override void visit_property (Bala.Property prop) {
		write_node (prop);
	}
}

